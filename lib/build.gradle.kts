import java.util.Properties
import java.util.Date

plugins {
    kotlin("jvm") version "1.5.0"
    id("com.github.ben-manes.versions") version "0.38.0"
    id("fr.coppernic.versioning") version "3.2.1"
    id("org.jetbrains.dokka") version "1.4.32"
    id("com.jfrog.bintray") version "1.8.5"
    `maven-publish`
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    implementation("com.google.code.gson:gson:2.8.6")

    testImplementation("junit:junit:4.13.2")
}

tasks {
//    dokka {
//        outputFormat = "html"
//        outputDirectory = "$buildDir/javadoc"
//        moduleName = rootProject.name
//    }

    publishToMavenLocal {
        dependsOn(build)
    }
}

// val dokkaJar by tasks.creating(Jar::class) {
//    group = JavaBasePlugin.DOCUMENTATION_GROUP
//    description = "Assembles Kotlin docs with Dokka"
//    archiveClassifier.set("javadoc")
//    from(tasks.dokka)
//    dependsOn(tasks.dokka)
// }

val sourcesJar by tasks.creating(Jar::class) {
    archiveClassifier.set("sources")
    from(sourceSets.getByName("main").allSource)
}

val artifactName = "geojson-kotlin"
val artifactGroup = "org.chrishatton.lib"

val pomUrl = "https://github.com/chris-hatton/geojson-kotlin"
val pomScmUrl = "https://github.com/chris-hatton/geojson-kotlin"
val pomIssueUrl = "https://github.com/chris-hatton/geojson-kotlin/issues"
val pomDesc = "https://github.com/chris-hatton/geojson-kotlin"

val githubRepo = "chris-hatton/geojson-kotlin"
val githubReadme = "README.md"

val pomLicenseName = "The Apache Software License, Version 2.0"
val pomLicenseUrl = "http://www.apache.org/licenses/LICENSE-2.0.txt"
val pomLicenseDist = "repo"

val pomDeveloperId = "chris-hatton"
val pomDeveloperName = "Chris Hatton"

versioning {
    releaseMode = "snapshot"
}

publishing {
    publications {
        create<MavenPublication>("lib") {
            groupId = artifactGroup
            artifactId = artifactName
            version = project.versioning.info.full
            from(components["java"])
            // artifact(dokkaJar)
            artifact(sourcesJar)

            pom.withXml {
                asNode().apply {
                    appendNode("description", pomDesc)
                    appendNode("name", rootProject.name)
                    appendNode("url", pomUrl)
                    appendNode("licenses").appendNode("license").apply {
                        appendNode("name", pomLicenseName)
                        appendNode("url", pomLicenseUrl)
                        appendNode("distribution", pomLicenseDist)
                    }
                    appendNode("developers").appendNode("developer").apply {
                        appendNode("id", pomDeveloperId)
                        appendNode("name", pomDeveloperName)
                    }
                    appendNode("scm").apply {
                        appendNode("url", pomScmUrl)
                    }
                }
            }
        }
    }
}

bintray {

    val properties = Properties()
    val inputStream = project.rootProject.file("local.properties").inputStream()
    properties.load(inputStream)

    user = System.getenv("bintrayUser") ?: properties.getProperty("bintrayUser")
    key = System.getenv("bintrayKey") ?: properties.getProperty("bintrayKey")
    publish = true

    setPublications("lib")

    pkg.apply {
        repo = "lib"
        name = rootProject.name
        setLicenses("Apache-2.0")
        setLabels("Gson", "json", "GeoJson", "GPS", "Kotlin")
        vcsUrl = pomScmUrl
        websiteUrl = pomUrl
        issueTrackerUrl = pomIssueUrl
        githubRepo = githubRepo
        githubReleaseNotesFile = githubReadme

        version.apply {
            name = project.versioning.info.full
            desc = pomDesc
            released = Date().toString()
            vcsTag = project.versioning.info.tag
        }
    }
}
